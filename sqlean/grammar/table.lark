?table_item: table_name
	| table_item_aliased

?table_name: STANDARD_TABLE_NAME
	| dbt_table_name

table_item_aliased: table_name as_alias

STANDARD_TABLE_NAME: /\w+|\{\{\s*\w+\s*\}\}/
	| /(`\w+`|\w+|`\{\{\s*\w+\s*\}\}`|\{\{\s*\w+\s*\}\})\.(`\w+`|\w+|`\{\{\s*\w+\s*\}\}`|\{\{\s*\w+\s*\}\})\.(`\w+`|\w+|`\{\{\s*\w+\s*\}\}`|\{\{\s*\w+\s*\}\})/
    | /`(\w+|\{\{\s*\w+\s*\}\})\.(\w+|\{\{\s*\w+\s*\}\})\.(\w+|\{\{\s*\w+\s*\}\})`/
// Regex is needed above to have a single token. These are of the form:
// 1. "foo" or "{{ foo }}"
// 2. "A.B.C" where A, B and C can be of the form: "`foo`", "foo", "`{{ foo }}`" or "{{ foo }}"
// 3. "`A.B.C`" where A, B and C can be of the form: "foo" or "{{ foo }}"

TABLE_REF_FIELD_NAME: /(\w+|\{\{\s*\w+\s*\}\})\.(\w+|\{\{\s*\w+\s*\}\})/
	| /(`\w+`|\w+|`\{\{\s*\w+\s*\}\}`|\{\{\s*\w+\s*\}\})\.(`\w+`|\w+|`\{\{\s*\w+\s*\}\}`|\{\{\s*\w+\s*\}\})\.(`\w+`|\w+|`\{\{\s*\w+\s*\}\}`|\{\{\s*\w+\s*\}\})\.(\w+|\{\{\s*\w+\s*\}\})/
    | /`(\w+|\{\{\s*\w+\s*\}\})\.(\w+|\{\{\s*\w+\s*\}\})\.(\w+|\{\{\s*\w+\s*\}\})`\.(\w+|\{\{\s*\w+\s*\}\})/
// Regex is needed above to have a single token. These are of the form:
// 1. "C.D" where C and D can be of the form "foo" or "{{ foo }}"
// 2. "A.B.C.D" where A, B and C can be of the form: "`foo`", "foo", "`{{ foo }}`" or "{{ foo }}" and D can be of the form "foo" or "{{ foo }}"
// 3. "`A.B.C`.D" where A, B, C and D can be of the form: "foo" or "{{ foo }}"

%import common.CNAME
%import alias.as_alias
%import jinja.dbt_table_name
