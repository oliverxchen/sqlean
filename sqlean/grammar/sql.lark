?start: query_file

query_file: query_expr

!query_expr: select_statement select_list ["FROM"i from_clause] [limit_clause]

!select_statement: "SELECT"i
!from_clause: from_item ["WHERE"i bool_list] [groupby_modifier] [orderby_modifier]

!?from_item: join_operation
    | table_item
    | sub_query_expr

sub_query_expr: "(" query_expr ")"

?join_operation: cross_join_operation
    | join_operation_with_condition

cross_join_operation: from_item cross_join from_item
join_operation_with_condition: from_item join_type from_item (on_clause | using_clause)

cross_join: "CROSS"i "JOIN"i

?join_type: inner_join
    | full_join
    | left_join
    | right_join

inner_join: ["INNER"i] "JOIN"i
full_join: "FULL"i ["OUTER"i] "JOIN"i
left_join: "LEFT"i ["OUTER"i] "JOIN"i
right_join: "RIGHT"i ["OUTER"i] "JOIN"i

!on_clause: "ON"i bool_list
!using_clause: "USING"i "(" using_list ")"
using_list: CNAME ("," CNAME)*

!groupby_modifier: "GROUP"i "BY"i groupby_list //[having_clause]
groupby_list: groupby_item ("," groupby_item)*
groupby_item: base_expression | INT

// !having_clause: "HAVING"i bool_list

!orderby_modifier: "ORDER"i "BY"i orderby_list
orderby_list: orderby_item ("," orderby_item)*
!orderby_item: (base_expression | INT) ["ASC"i | "DESC"i]

!limit_clause: "LIMIT"i INT ["OFFSET"i INT]

%import expression.select_list
%import expression.bool_list
%import expression.base_expression
%import table.table_item
%import common.WS
%import common.CNAME
%import common.INT
%ignore WS
