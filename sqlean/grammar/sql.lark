?start: query_file

query_file: query_expr

!query_expr: select_statement select_list ["FROM"i from_clause] [limit_clause]

!select_statement: "SELECT"i
!from_clause: from_item ["WHERE"i where_list] [groupby_modifier] [orderby_modifier]

!?from_item: table_name
    | join_operation
    | sub_query_expr
    // | (join_operation table_name)* //(on_clause)*)*

sub_query_expr: "(" query_expr ")"

!groupby_modifier: "GROUP"i "BY"i groupby_list //[having_clause]
groupby_list: groupby_item ("," groupby_item)*
groupby_item: base_expression | INT

// !having_clause: "HAVING"i where_list

!orderby_modifier: "ORDER"i "BY"i orderby_list
orderby_list: orderby_item ("," orderby_item)*
!orderby_item: (base_expression | INT) ["ASC"i | "DESC"i]

?table_name: simple_table_name
    | explicit_table_name

simple_table_name: CNAME
explicit_table_name: "`" PROJECT_ID  "." DATASET_ID "." TABLE_ID "`"
    | "`" PROJECT_ID  "`.`" DATASET_ID "`.`" TABLE_ID "`"

PROJECT_ID: CNAME
DATASET_ID: CNAME
TABLE_ID: CNAME

?join_operation: cross_join_operation
    | join_operation_with_condition

cross_join_operation: from_item cross_join from_item
join_operation_with_condition: from_item join_type from_item //( on_clause | using_clause)

cross_join: "CROSS"i "JOIN"i

?join_type: inner_join
    | full_join
    | left_join
    | right_join

inner_join: ["INNER"i] "JOIN"i
full_join: "FULL"i ["OUTER"i] "JOIN"i
left_join: "LEFT"i ["OUTER"i] "JOIN"i
right_join: "RIGHT"i ["OUTER"i] "JOIN"i

// on_clause:
//     ON bool_expression

// using_clause:
//     USING ( join_column [, ...] )

!limit_clause: "LIMIT"i INT ["OFFSET"i INT]

%import expression.select_list
%import expression.where_list
%import expression.base_expression
// %import expression.as_alias
%import common.WS
%import common.CNAME
%import common.INT
%ignore WS
