///////////////////
// Jinja symbols //
///////////////////
VAR_START_STR: "{{"
VAR_END_STR: "}}"
BLOCK_START_STR: "{%"
BLOCK_END_STR: "%}"

////////////////
// dbt basics //
////////////////
!dbt_config: VAR_START_STR CONFIG "(" [python_arg_list] ")" VAR_END_STR
CONFIG: "config"

!dbt_ref: VAR_START_STR "ref" "(" dbt_reference ")" VAR_END_STR
!dbt_src: VAR_START_STR "source" "(" source_id "," table_id [","] ")" VAR_END_STR
dbt_reference: string
source_id: string
table_id: string
dbt_table_name: dbt_ref
    | dbt_src

///////////
// jinja //
///////////
!macro: VAR_START_STR CNAME "(" [python_arg_list] ")" VAR_END_STR
!jinja_set_block: BLOCK_START_STR "set" CNAME "=" python_arg_value BLOCK_END_STR
jinja_variable: VAR_START_STR CNAME VAR_END_STR

/////////////////
// python args //
/////////////////
!python_arg_list: python_arg_item ("," python_arg_item)* [","]
python_arg_item: arg_name python_arg_value
    | python_arg_value
!?arg_name: CNAME "="
!python_arg_value: literal
    | "[" python_arg_list "]"
    | "{" python_dict_list "}"
    | CNAME "(" [python_arg_list] ")"
!python_dict_list: python_dict_item ("," python_dict_item)* [","]
!python_dict_item: string ":" python_arg_value


%import common.CNAME
%import literal.literal
%import literal.string
